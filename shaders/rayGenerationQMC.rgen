#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_scalar_block_layout : enable

#include "common.glsl"

// Ray generation shader (Quasi-Monte Carlo)

const int MAX_DEPTH = 10;

const int SAMPLING_DIMENSIONS = 2 + 3 * MAX_DEPTH; // 2 for antialiasing, 3 per each depth of ray tracing

layout(binding = 0) uniform accelerationStructureEXT tlas;  // Acceleration structure (scene)
layout(binding = 1, rgba32f) writeonly uniform image2D targetImage; // Image to store rendering result
layout(binding = 2) uniform Uniforms {
  RayTracingUniform uniforms;
};

layout(binding = 11, scalar) readonly buffer HammersleySequence {
  float[] hammersley;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

float getHammersley(int sampleId, int dim)
{
  return hammersley[SAMPLING_DIMENSIONS * sampleId + dim];
}

void main()
{
  vec3 meanColor = vec3(0.0);

  for (int sampleId = 0; sampleId < uniforms.samplesPerPixel; sampleId++) {
    // Random jitter added to pixel coordinate for antialiasing
    vec2 jitter = vec2(getHammersley(sampleId, 0), getHammersley(sampleId, 1));
    // Pixel position in normalized device coordinate (-1 <= x,y <= 1)
    vec2 pixelNDC = 2.0 * (vec2(gl_LaunchIDEXT.xy) + jitter) / vec2(gl_LaunchSizeEXT.xy) - 1.0;
    // Ray direction in camera coordinate
    vec4 directionCam = uniforms.invProjectionMat * vec4(pixelNDC, 1.0, 1.0);
    // Ray direction in world coordinate
    vec3 direction = (uniforms.invViewMat * directionCam).xyz;
    // Ray origin (camera position) in world coordinate
    vec3 origin = (uniforms.invViewMat * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

    int depth = 0;
    payload.color = vec3(1.0);

    int dim = 2;

    do {
      // Generate random numbers used in the closest hit shader
      for (int i = 0; i < payload.random.length(); i++) {
        payload.random[i] = getHammersley(sampleId, dim);
        dim++;
      }

      float tMin = 0.001;
      float tMax = 10000.0;

      traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, origin, tMin, direction, tMax, 0);

      origin = payload.nextOrigin;
      direction = payload.nextDirection;

      depth++;
    } while (payload.traceNextRay && depth < MAX_DEPTH);

    meanColor = (sampleId * meanColor + payload.color) / (sampleId + 1); 
  }

  // Gamma correction
  vec3 correctedColor = sqrt(meanColor);

  imageStore(targetImage, ivec2(gl_LaunchIDEXT.xy), vec4(correctedColor, 1.0));
}
