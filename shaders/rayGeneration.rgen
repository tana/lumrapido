#version 460
#extension GL_EXT_ray_tracing : enable

#include "common.glsl"

// Ray generation shader

const int MAX_DEPTH = 10;

layout(binding = 0) uniform accelerationStructureEXT tlas;  // Acceleration structure (scene)
layout(binding = 1, rgba32f) writeonly uniform image2D targetImage; // Image to store rendering result
layout(binding = 2) uniform Uniforms {
  RayTracingUniform uniforms;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

RandomState state;

void main()
{
  // Initialize RNG using pixel coord as seed
  initRandom(state, (gl_LaunchIDEXT.x << 16) | gl_LaunchIDEXT.y);

  vec3 meanColor = vec3(0.0);

  for (int i = 0; i < uniforms.samplesPerPixel; i++) {
    // Random jitter added to pixel coordinate for antialiasing
    vec2 jitter = vec2(randomFloat(state, 0.0, 1.0), randomFloat(state, 0.0, 1.0));
    // Pixel position in normalized device coordinate (-1 <= x,y <= 1)
    vec2 pixelNDC = 2.0 * (vec2(gl_LaunchIDEXT.xy) + jitter) / vec2(gl_LaunchSizeEXT.xy) - 1.0;
    // Ray direction in camera coordinate
    vec4 directionCam = uniforms.invProjectionMat * vec4(pixelNDC, 1.0, 1.0);
    // Ray direction in world coordinate
    vec3 direction = (uniforms.invViewMat * directionCam).xyz;
    // Ray origin (camera position) in world coordinate
    vec3 origin = (uniforms.invViewMat * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

    int depth = 0;
    payload.color = vec3(1.0);

    do {
      // Generate random numbers used in the closest hit shader
      for (int i = 0; i < payload.random.length(); i++) {
        payload.random[i] = randomFloat(state, 0.0, 1.0);
      }

      float tMin = 0.001;
      float tMax = 10000.0;

      traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, origin, tMin, direction, tMax, 0);

      origin = payload.nextOrigin;
      direction = payload.nextDirection;

      depth++;
    } while (payload.traceNextRay && depth < MAX_DEPTH);

    meanColor = (i * meanColor + payload.color) / (i + 1); 
  }

  // Gamma correction
  vec3 correctedColor = sqrt(meanColor);

  imageStore(targetImage, ivec2(gl_LaunchIDEXT.xy), vec4(correctedColor, 1.0));
}