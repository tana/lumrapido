#version 460
#extension GL_EXT_ray_tracing : enable

// Ray generation shader

const int MAX_DEPTH = 10;

// State for Xorshift random number generator
struct RandomState
{
  uint x, y, z, w;
};

layout(binding = 0) uniform accelerationStructureEXT tlas;  // Acceleration structure (scene)
layout(binding = 1, rgba32f) uniform image2D targetImage; // Image to store rendering result
layout(binding = 2) uniform RayTracingUniform {
  mat4 invViewMat; // Inverse of view matrix (i.e. transform camera coordinate to world coordinate)
  mat4 invProjectionMat; // Inverse of projection matrix (i.e. transform normalized device coordinate into camera coordinate)
  uint samplesPerPixel; // How many rays are sampled to render one pixel
} uniforms;

layout(location = 0) rayPayloadEXT RayPayload {
  vec3 color;
  bool traceNextRay;
  vec3 nextOrigin;
  vec3 nextDirection;
  RandomState randomState;
} payload;

// Pseudo-random number using Xorshift (xor128)
// G. Marsaglia, "Xorshift RNGs", Journal of Statistical Software, vol. 8, no. 14, pp. 1-6, 2003, doi: 10.18637/jss.v008.i14
uint random(inout RandomState state)
{
  uint t = (state.x ^ (state.x << 11));
  state.x = state.y;
  state.y = state.z;
  state.z = state.w;
  return (state.w = (state.w ^ (state.w >> 19)) ^ (t ^ (t >> 8)));
}

// Initialize the RNG.
void initRandom(out RandomState state, uint x)
{
  state.x = x;
  // Values of y, z, w are same as Marsaglia's paper.
  state.y = 362436069;
  state.z = 521288629;
  state.w = 88675123;
}

float randomFloat(inout RandomState state, float minimum, float maximum)
{
  return minimum + (float(random(state)) / 4294967296.0) * (maximum - minimum);
}

void main()
{
  // Initialize RNG using pixel coord as seed
  initRandom(payload.randomState, (gl_LaunchIDEXT.x << 16) | gl_LaunchIDEXT.y);

  vec3 meanColor = vec3(0.0);

  for (int i = 0; i < uniforms.samplesPerPixel; i++) {
#if 1
    // Random jitter added to pixel coordinate for antialiasing
    vec2 jitter = vec2(randomFloat(payload.randomState, 0.0, 1.0), randomFloat(payload.randomState, 0.0, 1.0));
    // Pixel position in normalized device coordinate (-1 <= x,y <= 1)
    vec2 pixelNDC = 2.0 * (vec2(gl_LaunchIDEXT.xy) + jitter) / vec2(gl_LaunchSizeEXT.xy) - 1.0;
    // Ray direction in camera coordinate
    vec4 directionCam = uniforms.invProjectionMat * vec4(pixelNDC, 1.0, 1.0);
    // Ray direction in world coordinate
    vec3 direction = (uniforms.invViewMat * directionCam).xyz;
    // Ray origin (camera position) in world coordinate
    vec3 origin = (uniforms.invViewMat * vec4(0.0, 0.0, 0.0, 1.0)).xyz;
#else
    float focalLength = 1.0;
    float viewportHeight = 2.0;
    float viewportWidth = viewportHeight * float(gl_LaunchSizeEXT.x) / float(gl_LaunchSizeEXT.y);
    float u = (float(gl_LaunchIDEXT.x) + randomFloat(payload.randomState, 0.0, 1.0)) / float(gl_LaunchSizeEXT.x - 1);
    float v = 1.0 - (float(gl_LaunchIDEXT.y) + randomFloat(payload.randomState, 0.0, 1.0)) / float(gl_LaunchSizeEXT.y - 1);
    // Point on the image plane
    float x = viewportWidth * u - viewportWidth / 2.0;
    float y = viewportHeight * v - viewportHeight / 2.0;
  
    vec3 origin = vec3(0.0, 0.0, 0.0);
    vec3 direction = vec3(x, y, -1.0);
#endif

    int depth = 0;
    payload.color = vec3(1.0);

    do {
      float tMin = 0.001;
      float tMax = 10000.0;

      traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, origin, tMin, direction, tMax, 0);

      origin = payload.nextOrigin;
      direction = payload.nextDirection;

      depth++;
    } while (payload.traceNextRay && depth < MAX_DEPTH);

    meanColor = (i * meanColor + payload.color) / (i + 1); 
  }

  // Gamma correction
  vec3 correctedColor = sqrt(meanColor);

  imageStore(targetImage, ivec2(gl_LaunchIDEXT.xy), vec4(correctedColor, 1.0));
}